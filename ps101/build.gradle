if (!isLibrary.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    namespace "com.moko.ps101m"
    compileSdk 34

    defaultConfig {
        minSdk 26
        targetSdk 34
        versionCode 7
        versionName "1.0.7"

        def formatDate = new Date().format("yyyyMMddHHmm")
        setProperty("archivesBaseName", "PS101M_${versionName}_${formatDate}")
    }

    buildTypes {
        release {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
        debug {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    viewBinding {
        enabled = true
    }

    lintOptions {
        disable 'SetTextI18n'
        checkReleaseBuilds false
        abortOnError false
    }

    sourceSets {
        main {
            if (!isLibrary.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }
        }
    }

    if (isLibrary.toBoolean()) {
        libraryVariants.all { variant ->
            if (variant.buildType.name == "debug") {
                // 修改aar名称
                variant.outputs.all { output ->
                    def formatDate = new Date().format("yyyyMMddHHmm")
                    outputFileName = "PS101M_${android.defaultConfig.versionName}_${formatDate}.aar"
                }
                // 复制aar到指定目录
                variant.assemble.doLast {
                    variant.outputs.all { output ->
                        // 输出目录在：项目目录/build/outputs/aar/
                        def outputPath = [project.rootDir.absolutePath, "build", "outputs", "aar"].join(File.separator)
                        copy {
                            from output.outputFile
                            into outputPath
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation files('libs/jxl.jar')
    if (!isLibrary.toBoolean()) {
        implementation project(path: ':mokosupport')
        implementation 'com.github.MOKO-Android-Base-Library:MKLoRaUILib:1.0.9'
    } else {
        compileOnly 'com.github.MOKO-Android-Base-Library:MKLoRaUILib:1.0.9'
        compileOnly files('libs/mokoBleLib.jar')
        implementation files('libs/mokoSupport.jar')
        // mokosupport
        implementation 'no.nordicsemi.android.support.v18:scanner:1.6.0'
        implementation 'no.nordicsemi.android:dfu:2.4.1'
        implementation 'org.greenrobot:eventbus:3.3.1'
        implementation 'com.elvishew:xlog:1.6.1'
        implementation 'no.nordicsemi.android:ble:2.7.3'
        implementation 'androidx.appcompat:appcompat:1.6.1'
    }
}
